//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////  NEXT LINES ARE HERE ONLY BECAUSE WE'RE BUILDING THE PLUGIN WITHIN THE SAME MULTIMODULE PROJECT            ////////
//////  That is: we must be sure the plugin is built before we use it                                             ////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


//Let's import the custom plugin we defined in buildSrc of the root project
import com.graphql_java_generator.gradle_task.StartApp
import com.graphql_java_generator.gradle_task.StopApp


buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		// Dependencies on the GraphQL Plugin. Replace this version by the release you want to use.
		classpath group: "com.graphql-java-generator", name: 'graphql-gradle-plugin', version: "${project.version}"
	}
}


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////  START OF THE Basic Client Gradle configuration to use the GraphQL Generator plugin                        ////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'graphql-gradle-plugin'

dependencies {

	// All the GraphQL dependencies (no dependencies for the GraphQL Gradle Plugin itself)
	compile "${project.group}:graphql-java-client-dependencies:${project.version}"

	// Use JUnit test framework
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.0'
	testRuntimeOnly 'org.apache.logging.log4j:log4j-jul:2.11.2'

}

// Let's configure the GraphQL Gradle Plugin
graphql {
	mode = "client"  //This line is here only for the demo, as client is the default mode
	packageName = "com.graphql_java_generator.samples.forum.client.graphql.forum.client"
}

// The next lines makes the GraphQL plugin be executed before Java compiles, so that all sources are generated on time
compileJava.dependsOn graphqlGenerateCode



//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////  NEXT LINES ARE HERE ONLY TO ALLOW THE INTEGRATION TESTS AGAINST THE SERVER GENERATED BY GraphQL Generator ////////
//////  That is: this project is both a sample, and an integration test                                           ////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

test {
	// enable JUnit Platform (a.k.a. JUnit 5) support
	useJUnitPlatform()
}


task startWebApp(type: StartApp) {
	jarFile = file("../${project.name}/build/libs/${project.name}_springboot-${project.version}.jar".replaceAll('client', 'server'))
	url = 'http://localhost:8180/graphiql'

	// The next lines insures that the Basic-server is built before starting the integration test from the client.
	dependsOn ':graphql-gradle-plugin-samples-Forum-server:assemble'

	// The stopWebApp must be attached here. And must run after tests
	finalizedBy 'stopWebApp'
}

task stopWebApp(type: StopApp) {
	//	The line blow is executed during configuration phase. So the process is unknown. 
	// We just define the Task object. The StopApp task will read the process id at execution time
	startAppTask = tasks["startWebApp"]

	// The stopWebApp is attached to startWebApp, and must run after tests
	mustRunAfter test
}

// The next lines insures that the Basic-server is built before starting the integration test from the client.
// Using mustRunAfter instead of dependsOn allows to manually start the server, then just execute the test task.
test.dependsOn startWebApp


configurations {
	// There has been an issue with stack overflow, because of redondant SLF4J implementation, when executing JUnit tests
	// It seems to be a random issue : gradle builds are not consistent !  :-((
	runtime.exclude group: "org.apache.logging.log4j", module: "log4j-to-slf4j"
	compile.exclude group: "org.apache.logging.log4j", module: "log4j-to-slf4j"
}
